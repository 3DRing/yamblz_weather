import groovy.json.JsonSlurper

def fileName = 'app-credentials.json'
def appStorePassword = (new JsonSlurper().parseText(file(fileName).text)).storePassword
def appKeyAlias = (new JsonSlurper().parseText(file(fileName).text)).keyAlias
def appKeyPassword = (new JsonSlurper().parseText(file(fileName).text)).keyPassword
def apiKey = (new JsonSlurper().parseText(file(fileName).text)).apiKey

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.7.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'
    defaultConfig {
        applicationId 'tljfn.yamblzweather'
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "0.3"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file('yamblz-weather-keystore.jks')
            keyAlias appKeyAlias
            keyPassword appKeyPassword
            storePassword appStorePassword
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            testCoverageEnabled = true
        }
        buildTypes.each {
            it.buildConfigField "String", "WEATHER_API_KEY", "\"" + apiKey + "\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    configurations.all {
        // for using espresso library
        resolutionStrategy.force "com.google.code.findbugs:jsr305:$findbugs_version"
    }
    lintOptions {
        // in order to be able to set release and debug strings separately
        disable 'MissingTranslation', 'ExtraTranslation'
    }
}

dependencies {
    // Debug
    debugCompile "com.facebook.stetho:stetho:$stetho_version"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$stetho_version"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseCompile "net.igenius:stetho-no-op:$stetho_noop_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    // Google architecture components
    compile "android.arch.lifecycle:runtime:$arch_version"
    compile "android.arch.lifecycle:extensions:$arch_version"
    compile "android.arch.persistence.room:runtime:$arch_version"
    compile "android.arch.persistence.room:rxjava2:$arch_version"
    annotationProcessor "android.arch.lifecycle:compiler:$arch_version"
    annotationProcessor "android.arch.persistence.room:compiler:$arch_version"

    // butterknife
    compile "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    //network
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // RxJava
    compile "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    compile "io.reactivex.rxjava2:rxjava:$rxjava_version"

    // Dagger
    compile "com.google.dagger:dagger:$dagger_version"
    compile "com.google.dagger:dagger-android:$dagger_version"
    compile "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // UI
    compile "com.android.support:design:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    compile "com.github.bumptech.glide:glide:$glide_version"

    //android-job
    compile "com.evernote:android-job:$android_job_version"
    compile "com.google.android.gms:play-services-gcm:$google_version"

    // google places
    compile "com.google.android.gms:play-services-places:$google_version"

    // preferences
    compile "com.android.support:preference-v14:$support_version"
    compile "com.android.support:preference-v7:$support_version"

    // tests
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "junit:junit:$junit_version"
    testCompile "org.assertj:assertj-core:$assertj_version"
    testCompile "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testCompile("android.arch.core:core-testing:$arch_version", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    })
}
